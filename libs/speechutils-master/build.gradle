buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 1
def versionMajorMinorPatch = "${versionMajor}.${versionMinor}.${versionPatch}"

ext {
    bintrayRepo = 'maven'
    bintrayName = 'AlexaAndroid'

    publishedGroupId = 'com.willblaschko.android.alexa'
    libraryName = 'AlexaAndroid'
    artifact = 'AlexaAndroid'

    libraryDescription = 'A library to abstract access to the Amazon Alexa service for Android applications.'

    siteUrl = 'https://github.com/willblaschko/AlexaAndroid'
    gitUrl = 'https://github.com/willblaschko/AlexaAndroid.git'

    libraryVersion = versionMajorMinorPatch

    developerId = 'willblaschko'
    developerName = 'Will Blaschko'
    developerEmail = 'will.blaschko@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 100
        versionName versionMajorMinorPatch
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
